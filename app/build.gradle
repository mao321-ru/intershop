import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "org.openapi.generator" version "7.12.0"
}

version = '0.0.3-SNAPSHOT'

// используем в jar-файле вместо webapp
archivesBaseName = 'intershop'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets.main.java.srcDir "$projectDir/build/generated/src/main/java"

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    runtimeOnly 'org.postgresql:r2dbc-postgresql'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:r2dbc'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'com.redis:testcontainers-redis:2.2.2'
}

tasks.register('buildClient', GenerateTask) {
    generatorName.set("java")
    inputSpec.set("$projectDir/../paysrv/src/main/resources/api-spec.yaml")
    outputDir.set("$projectDir/build/generated")
    ignoreFileOverride.set(".openapi-generator-java-sources.ignore")
    modelPackage.set("com.example.payclient.domain")
    invokerPackage.set("com.example.payclient")
    apiPackage.set("com.example.payclient.api")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "true",
            openApiNullable        : "false",
            serializableModel      : "true"
    ])
}

tasks.named('compileJava') {
    dependsOn(tasks.buildClient)
}
